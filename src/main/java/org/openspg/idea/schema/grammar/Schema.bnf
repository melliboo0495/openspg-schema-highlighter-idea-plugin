{
    parserClass="org.openspg.idea.lang.parser.SchemaParser"
    parserUtilClass="org.openspg.idea.schema.grammar.SchemaParserUtil"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Schema"
    psiImplClassSuffix="Impl"
    psiPackage="org.openspg.idea.lang.psi"
    psiImplPackage="org.openspg.idea.lang.psi.impl"

    elementTypeHolderClass="org.openspg.idea.grammar.psi.SchemaTypes"
    elementTypeClass="org.openspg.idea.schema.SchemaElementType"
    tokenTypeClass="org.openspg.idea.schema.psi.SchemaTokenType"

    psiImplUtilClass="org.openspg.idea.schema.psi.impl.SchemaPsiImplUtil"
}

schemaFile ::= item_*

private item_ ::= namespace|entity|DOCUMENT_END|COMMENT|indent_|EOL|TEXT|CONSTANT

private indent_ ::= INDENT|INDENT_META|INDENT_PROP|INDENT_PROPMETA|INDENT_SUBPROP|INDENT_SUBPROPMETA

private plain_text_block ::= (OPEN_PLAIN_BLOCK (TEXT|EOL)* CLOSE_PLAIN_BLOCK)
private text_value ::= TEXT | plain_text_block | CONSTANT
private EOL_ ::= (COMMENT | indent_)* EOL

namespace ::= NAMESPACE_MARKER TEXT EOL_ {
    methods=[getValue]
}

entity ::= entity_info EOL_* (INDENT_META entity_meta)* {
    methods=[getName]
}

entity_info ::= ENTITY_NAME OPEN_BRACKET ENTITY_ALIAS_NAME CLOSE_BRACKET (basedEntityClass_|inheritedEntityClass_) {
    mixin="org.openspg.idea.schema.psi.impl.SchemaNamedElementImpl"
    implements="org.openspg.idea.schema.psi.SchemaNamedElement"
    methods=[getEntityName getEntityAliasName getEntityClassList getName setName getNameIdentifier]
}

private basedEntityClass_ ::= COLON ENTITY_BUILDIN_CLASS
private inheritedEntityClass_ ::= INHERITED ((ENTITY_BUILDIN_CLASS|ENTITY_CLASS) COMMA)* (ENTITY_BUILDIN_CLASS|ENTITY_CLASS) COLON

entity_meta ::= (META_TYPE COLON text_value*) EOL_* (INDENT_PROP property)* {
    methods=[getName getValue]
}


property ::= property_info EOL_* (INDENT_PROPMETA property_meta EOL_*)*

private propertyClass_ ::= (BUILDIN_TYPE | PROPERTY_CLASS)
private multiPropertyClass_ ::= (propertyClass_ COMMA)* propertyClass_
property_info ::= (PROPERTY_NAME OPEN_BRACKET PROPERTY_ALIAS_NAME CLOSE_BRACKET COLON multiPropertyClass_) {
    methods=[getPropertyName getPropertyAliasName getPropertyClassList]
}

property_meta ::= (META_TYPE COLON text_value*) EOL_* (INDENT_SUBPROP sub_property)* {
    methods=[getName getValue]
}


sub_property ::= sub_property_info EOL_* (INDENT_SUBPROPMETA sub_property_meta EOL_*)*
sub_property_info ::= (PROPERTY_NAME OPEN_BRACKET PROPERTY_ALIAS_NAME CLOSE_BRACKET COLON multiPropertyClass_) {
    methods=[getPropertyName getPropertyAliasName getPropertyClassList]
}
sub_property_meta ::= (META_TYPE COLON text_value*) {
    methods=[getName getValue]
}
